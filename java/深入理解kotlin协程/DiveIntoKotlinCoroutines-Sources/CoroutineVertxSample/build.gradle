buildscript {
  ext {
    kotlin_version = '1.3.61'
    vertx_version = '3.8.5'
  }

  repositories {
    maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
//    maven {
//      url "https://oss.sonatype.org/content/repositories/snapshots/"
//    }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'kotlin'

sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.bennyhuo.kotlin.coroutine.vertx.MainVerticle'

repositories {
  maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
  maven {
    url "https://oss.sonatype.org/content/repositories/3963/"
  }
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation "io.vertx:vertx-core:$vertx_version"
  implementation "io.vertx:vertx-web:$vertx_version"

  implementation "io.vertx:vertx-jdbc-client:$vertx_version"
  implementation "org.hsqldb:hsqldb:2.3.4"

  implementation "io.vertx:vertx-lang-kotlin:$vertx_version"
  implementation "io.vertx:vertx-lang-kotlin-coroutines:$vertx_version"
}

// Redeploy watcher.
run {
  args = ['run', mainVerticleName,
          "--launcher-class=$mainClassName",
          "--redeploy=src/**/*.*",
          "--on-redeploy=gradlew.bat classes"
  ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
  baseName = 'app'
  classifier = 'shadow'

  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

// Heroku relies on the 'stage' task to deploy.
task stage {
  dependsOn shadowJar
}
